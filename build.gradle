apply plugin: 'java'
apply plugin: 'application'

// editor plugin dependencies
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    jcenter()
}

// enable assertions by default
applicationDefaultJvmArgs = ["-ea"]

// files that need to be compiled
sourceSets.main.java.srcDirs  = [
    'src/main/java',
    'src/main/generated'
]

// default arguments for "gradlew run"
run {
    args "-gui"
}

// include files into the final distribution

distZip {
    into(project.name) {
        from '.'
        include 'examples/*'
        include 'tests/*'
        include 'LICENSE'
    }
}

distTar {
    into(project.name) {
        from '.'
        include 'examples/*'
        include 'tests/*'
        include 'LICENSE'
    }
}

// add antlr to the final distribution
dependencies {
    compile files('lib/antlr-runtime-4.5.1.jar')
}

// define the main class to start
mainClassName = "info.fulloo.trygve.editor.Main"
jar {
    manifest {
        attributes 'Main-Class': 'info.fulloo.trygve.editor.Main'
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task runJar (dependsOn:jar) << {
    javaexec {
        main = "-jar"
        args = [
                "-ea",
                "build/libs/trygve.jar",
                "-c",
                "./tests/andreas4.k",
                "-r"
        ]
    }
}

// invokes antlr to generate parser
task antlr(type: Exec) {
    workingDir 'src/main/java'

    if (System.getProperty('os.name').toLowerCase().contains('windows') ||
        System.getProperty('os.name').toLowerCase().contains('Windows') ) {
        commandLine 'java',
            '-Dline.separator=\n',
            '-jar', "$rootProject.projectDir\\lib\\antlr-4.5.1-complete.jar",
            '-o', "$rootProject.projectDir\\src\\main\\generated",
            'info\\fulloo\\trygve\\parser\\Kant.g4'
    } else {
        commandLine 'java',
            '-jar', "$rootProject.projectDir/lib/antlr-4.5.1-complete.jar",
            '-o', "$rootProject.projectDir/src/main/generated",
            'info/fulloo/trygve/parser/Kant.g4'
    }
}

// make compilation faster
compileJava {
    options.fork = true
    options.incremental = true
}
